<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009" 
					   xmlns:s="library://ns.adobe.com/flex/spark" 
					   xmlns:controls="bootstrap.controls.*" xmlns:ns="http://flex.apache.org/experimental/ns" 
					   xmlns:local="*" xmlns:com="com.*" xmlns:mx="library://ns.adobe.com/flex/mx"
					   creationComplete="onComplete(event)" addedToStage="windowedapplication1_addedToStageHandler(event)"
					   width="800" height="650" showStatusBar="false">
	<fx:Style source="custom.css"/>
	<s:states>
		<s:State name="normal"/>
		<s:State name="asistencia" id="stateAsistencia" />
	</s:states>
	<fx:Script>
		<![CDATA[
			import clases.Asistencias;
			import clases.Clases;
			import clases.ClasesAsistencia;
			import clases.Clientes;
			import clases.Config;
			import clases.Facturas;
			import clases.Grupos;
			import clases.Horarios;
			import clases.Instructores;
			import clases.Pagos;
			import clases.Productos;
			import clases.Salones;
			import clases.Tareas;
			import clases.Usuarios;
			
			import com.AcercaDe;
			import com.ListPicker;
			import com.Login;
			import com.Modal;
			import com.ModalAlert;
			
			import interfaces.IModal;
			
			import mx.collections.ArrayList;
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;
			
			import org.osflash.signals.natives.NativeSignal;
			
			import spark.primitives.BitmapImage;
			
			import sr.modulo.Modulo;
			
			import utils.DateUtil;
			
			import views.Inicio;
			import views.asistencias.AsistenciaRegistro;
			
			import vo.VOClaseAsistencia;
			import vo.VOHorario;
			import vo.VOUsuario;
			private static var KEYBOARD_STAGE:Boolean=true;

			public static var sql:Modulo;
			public static var menuSignal:NativeSignal;
			
			public static var grupos:Grupos;
			public static var clientes:Clientes;
			public static var horarios:Horarios;
			public static var productos:Productos;
			public static var pagos:Pagos;
			public static var facturas:Facturas;
			public static var salones:Salones;
			public static var instructores:Instructores;
			public static var clasess:Clases;
			public static var config:Config;
			public static var tareas:Tareas;
			public static var asistencias:Asistencias;
			public static var usuarios:Usuarios;
			
			[Embed(source="assets/gestion.sql",mimeType="application/octet-stream")]
			private var myFile:Class;
			
			[Embed(source="assets/logo.jpg")]
			private var logo:Class;
			
			protected function onComplete(event:FlexEvent):void {
				sql = new Modulo;
				var dbfile:File = File.applicationStorageDirectory.resolvePath("gestion.db");
				var exist:Boolean = dbfile.exists;
				sql.iniciar(dbfile);
				
				if (!exist) {
					var sqlText:ByteArray = new myFile;
					var lineas:Array = sqlText.readMultiByte(sqlText.bytesAvailable,File.systemCharset).split(File.lineEnding);
					for each (var s:String in lineas) {
						sql.sql(s);	
					}
				}
				var splash:ModalAlert = ModalAlert.showDelay("","Iniciando Sistema...",null,500,function ():void {
					showLogin(login_close);
				});
				config = new Config;
				grupos = new Grupos;
				clientes = new Clientes;
				horarios = new Horarios;
				productos = new Productos;
				pagos = new Pagos;
				facturas = new Facturas;
				salones = new Salones;
				instructores = new Instructores;
				clasess = new Clases;
				tareas = new Tareas;
				asistencias = new Asistencias;		
				usuarios = new Usuarios;
				
				splash.removeAllElements();
				var img:BitmapImage = new BitmapImage;
				img.source = new logo;
				splash.addElement(img);
				PopUpManager.centerPopUp(splash);
				
				btnAcerca.addEventListener(MouseEvent.CLICK,acerca_click);
				btnSesion.addEventListener(MouseEvent.CLICK,sesion_click);
				stateAsistencia.addEventListener(FlexEvent.ENTER_STATE,asistencia_enter);
				mainMenu.nav = nav;
				
				VOUsuario.usuarioChange.add(function (usuario:VOUsuario):void {
					if (usuario.acceso>0) {
						nav.removeAllElements();
						nav.addView("inicio_view",Inicio,null,true);
					}
				});
				
				Modal.globalPopUp.add(function (modal:IModal):void {
					KEYBOARD_STAGE=false;
				});
				Modal.globalPopOff.add(function (modal:IModal):void {
					KEYBOARD_STAGE=true;
				});
			}
			
			protected function acerca_click(event:MouseEvent):void {
				var m:AcercaDe = new AcercaDe;
				m.popUp();
			}
			
			protected function stage_keyDown(event:KeyboardEvent):void {
				if (KEYBOARD_STAGE) {
					if (event.keyCode==Keyboard.END) {
						currentState = "asistencia";
					} else if (event.keyCode==Keyboard.HOME) {
						currentState = "normal";
						nav.setFocus();
					} else if (event.keyCode==Keyboard.F1) {
						var picker:ListPicker = new ListPicker;
						picker.dataProvider = new ArrayList(nav.history);
						picker.labelField = "name";
						picker.onClose = viewsClose;
						picker.popUp();
					}
				}
			}
			
			protected function asistencia_enter(event:FlexEvent):void {
				nav.addView("asistencia_cliente",AsistenciaRegistro,{back:false},true);
			}
			
			protected function windowedapplication1_addedToStageHandler(event:Event):void {
				stage.addEventListener(KeyboardEvent.KEY_DOWN,stage_keyDown);
			}
			
			private function login_close():void {
				var hoy:Date = new Date;
				var hoyString:String = DateUtil.dateToString(hoy,"YYYY-MM-DD"); 
				var horariosDia:Vector.<VOHorario> = horarios.horariosDelDia(hoy);
				var clase:VOClaseAsistencia = new VOClaseAsistencia;
				for (var i:int = 0; i < horariosDia.length; i++) {
					if (ClasesAsistencia.instancia.existe(horariosDia[i].claseID,hoyString)==false) {
						clase.fecha = hoyString;
						clase.usuarioID = VOUsuario.USUARIO_ACTIVO.usuarioID;
						clase.claseID = horariosDia[i].claseID;
						
						ClasesAsistencia.instancia.insertar(clase);
					}
				}
				nav.addView("inicio_view",Inicio,null,true);
				this.setFocus();
			}
			
			override protected function stateChanged(oldState:String, newState:String, recursive:Boolean):void {
				if (newState!=oldState) {
					super.stateChanged(oldState,newState,recursive);
					if (newState=="normal" && oldState=="asistencia") {
						nav.removeView("asistencia_cliente",true);
						showLogin(function ():void {
							nav.showView(nav.lastView);
						});
					}
				}
			}
			
			private function showLogin(close:Function):void {
				var login:Login = new Login;
				login.onClose = close;
				login.popUp();
			}
			
			protected function sesion_click(event:MouseEvent):void {
				nav.removeView(nav.lastView);
				showLogin(function ():void {
					nav.showView(nav.lastView);
				});
			}
			
			private function viewsClose(indice:int,view:String):void {
				if (indice>-1)
					nav.showView(view);
				nav.setFocus();
			}
			
		]]>
	</fx:Script>
	<s:VGroup width="100%" height="100%" gap="0">
		<com:MainMenu id="mainMenu" width="100%" height="110" includeIn="normal"/>
		<local:ViewNavigatorHistory id="nav" width="100%" height="100%" top.asistencia="0" bottom="0"/>		
	</s:VGroup>
	<s:HGroup right="0" top="0" gap="0">
		<s:Button label="Cerrar SesiÃ³n" height="30" styleName="btn-warning" id="btnSesion" includeIn="normal"/>
		<s:Button label="Acerca" height="30" styleName="btn-menu" id="btnAcerca" includeIn="normal"/>
	</s:HGroup>
</s:WindowedApplication>
